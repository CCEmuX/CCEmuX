import groovy.json.JsonSlurper

plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '1.2.4'
	id 'eclipse'
	id 'idea'
}

group = 'net.clgd'
version = '1.0-SNAPSHOT'

mainClassName = 'net.clgd.ccemux.init.Launcher'

sourceCompatibility = 1.8
targetCompatibility = 1.8

eclipse.project {
	natures 'org.eclipse.buildship.core.gradleprojectnature'
	natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

project.ext.cc = new JsonSlurper().parse(file('src/main/resources/ccemux.json'))

repositories {
	mavenCentral()

	maven {
		name = 'computercraft'
		url = "https://cc.crzd.me/maven"
	}

	maven {
		name = "squiddev"
		url = "https://dl.bintray.com/squiddev/maven"
	}

	maven {
		name = "snapshots"
		url = "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.16.18'

	compileOnly "dan200.computercraft:ComputerCraft:${cc.ccRevision}"

	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
	compile 'org.apache.logging.log4j:log4j-core:2.8.2'

	compile 'commons-io:commons-io:2.5'
	compile 'commons-cli:commons-cli:1.3.1'
	compile 'commons-codec:commons-codec:1.10'
	compile 'org.apache.commons:commons-lang3:3.5'

	compile 'org.squiddev:cctweaks-lua:1.5.0-nightly-build0'

	compile 'com.google.guava:guava:20.0'
	compile 'com.google.code.gson:gson:2.8.0'

	testCompile 'junit:junit:4.12'
}

task customRom (type: Zip) {
	from 'src/main/rom'

	include '*'
	include '*/*'

	archiveName 'custom.rom'
	destinationDir = file('src/main/resources')
}

configure(customRom) {
	group = "Build"
	description = "Packs custom rom files into an embeddable archive."
}

processResources.dependsOn customRom

jar {
	manifest {
		attributes 'SplashScreen-Image': 'splash1.png', 'Implementation-Version': version
	}
}

shadowJar {
	classifier = null

	exclude '*.html'
	exclude 'META-INF/maven/*/*/*.*'
}

run {
	standardInput = System.in
}

runShadow {
	standardInput = System.in
}

build.dependsOn shadowJar
