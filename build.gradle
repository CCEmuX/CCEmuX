plugins {
	id 'application'
	id 'com.github.johnrengelman.shadow' version '2.0.1'
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	group = 'net.clgd'
	version = '1.0-SNAPSHOT'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	eclipse.project {
		natures 'org.eclipse.buildship.core.gradleprojectnature'
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
	}
	
	dependencies {
		compileOnly files("$rootProject.projectDir/lib/lombok-1.16.19.jar")
		testCompileOnly files("$rootProject.projectDir/lib/lombok-1.16.19.jar")
		
		compileOnly 'com.google.auto.service:auto-service:1.0-rc3'
	}
}

repositories {
	mavenCentral()

	maven {
		name = 'computercraft'
		url = "https://cc.crzd.me/maven"
	}

	maven {
		name = "squiddev"
		url = "https://dl.bintray.com/squiddev/maven"
	}

	maven {
		name = "snapshots"
		url = "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

dependencies {
	implementation project(':plugin-api')
	
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
	implementation 'org.apache.logging.log4j:log4j-core:2.8.2'

	implementation 'commons-io:commons-io:2.5'
	implementation 'commons-cli:commons-cli:1.4'
	implementation 'org.apache.commons:commons-lang3:3.6'

	implementation 'org.squiddev:cctweaks-lua:1.5.0-pr3'

	testCompile 'junit:junit:4.12'
}
	
mainClassName = 'net.clgd.ccemux.init.Launcher'

jar {
	manifest {
		attributes 'SplashScreen-Image': 'img/splash2.gif', 'Implementation-Version': version
	}
}

shadowJar {
	classifier = 'all'

	exclude '*.html'
	exclude 'META-INF/maven/*/*/*.*'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task noCCJar (type: ShadowJar) {
	classifier = 'no-cc'

	from sourceSets.main.output
	configurations = [
		project.configurations.runtimeClasspath
	]

	exclude '*.html'
	exclude 'META-INF/maven/*/*/*.*'

	dependencies {
		exclude(dependency('.*:ComputerCraft:.*'))
	}
}

run { standardInput = System.in }

runShadow { standardInput = System.in }

build.dependsOn shadowJar
build.dependsOn noCCJar
